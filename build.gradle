plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

ext {
  artifactId = "log4j-cloudwatch-appender"
}

group = "io.sensefly.logging.log4j"
version =  "0.0.1"
description = "Log4j 2.x appender for AWS CloudWatch logs"

apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "maven-publish"


sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

repositories {
  jcenter()
}

dependencies {
  compile "org.apache.logging.log4j:log4j-core:2.8.2"
  compile "com.amazonaws:aws-java-sdk-logs:1.11.161"

  testCompile "junit:junit:4.12"
  testCompile "org.assertj:assertj-core:3.8.0"
  testCompile "org.mockito:mockito-core:2.8.47"
}

jacoco {
  toolVersion = "0.7.9"
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = false
  }
}
check.dependsOn jacocoTestReport

// package source jar
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    BintrayPublication(MavenPublication) {
      from components.java
      artifact(sourcesJar) {
        classifier = "sources"
        groupId project.group
        artifactId project.artifactId
        version  project.version
      }
    }
  }
}

bintray {
  user = System.env.BINTRAY_USER
  key = System.env.BINTRAY_API_KEY
  publications = ['BintrayPublication']
  publish = true
  pkg {
    repo = 'maven'
    name = project.artifactId
    userOrg = 'sensefly'
    vcsUrl = 'https://github.com/sensefly-sa/log4j-cloudwatch-appender.git'
    version {
      name = project.version
      released  = new Date()
    }
  }
}
